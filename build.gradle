plugins {
    id 'groovy'
    id 'com.gradle.plugin-publish' version '1.1.0'

    id 'pl.allegro.tech.build.axion-release' version '1.14.4'
    id 'com.diffplug.spotless' version '6.15.0'
    id 'com.github.ben-manes.versions' version '0.46.0'
}

scmVersion {
    hooks {
        pre 'fileUpdate', [
            file: 'README.md',
            pattern: {v, c -> /$v/},
            replacement: {v, c ->
                "$v"
            }
        ]
        pre 'commit'
    }
    checks {
        aheadOfRemote = false
    }
    scmVersion {
        localOnly = true
    }
    ignoreUncommittedChanges = false
}

//tocme {
//    doc 'docs/ccv2-validation.md'
//}
//
//tasks.named('assemble') {
//    dependsOn('insertTocs')
//}
//tasks.named('checkTocs') {
//    mustRunAfter('insertTocs')
//}

spotless {
    format 'misc', {
        // define the files to apply `misc` to
        target '*.md', '.gitignore'

        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
    groovyGradle {
        target '*.gradle' // default target of groovyGradle
        greclipse().configFile('gradle/greclipse.properties')
    }
    java {
        target 'src/*/java/**/*.java'
        removeUnusedImports()
        importOrderFile('gradle/spotless.importorder')
        eclipse().configFile('gradle/spotless.xml')
    }
    groovy {
        target 'src/*/groovy/**/*.groovy'
        importOrderFile('gradle/spotless.importorder')
        greclipse().configFile('gradle/greclipse.properties')
    }
}

group = 'sap.commerce'
project.version = scmVersion.version

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform("org.spockframework:spock-bom:2.1-groovy-3.0")
    testImplementation "org.spockframework:spock-core"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}


gradlePlugin {
    website = 'https://github.com/SAP/commerce-gradle-plugin'
    vcsUrl = 'https://github.com/SAP/commerce-gradle-plugin'

    plugins {
        hybrisPlugin {
            id = 'sap.commerce.build'
            implementationClass = 'mpern.sap.commerce.build.HybrisPlugin'

            displayName = 'SAP Commerce Bootstrap & Build Plugin'
            description = """Manage the whole development lifecycle of your SAP Commerce Project with Gradle"""
            tags.set([
                'sap commerce',
                'sap hybris commerce',
                'hybris',
                'sap',
                'commerce',
                'bootstrap',
                'build'
            ])
        }
        ccv2BuildSupport {
            id = 'sap.commerce.build.ccv2'
            implementationClass = 'mpern.sap.commerce.ccv2.CloudV2Plugin'

            displayName = "SAP Commerce Cloud in the Public Cloud Build Support Plugin"
            description = """Use the CCv2 manifest.json to configure and build your local development environment"""
            tags.set([
                'sap commerce',
                'sap hybris commerce',
                'hybris',
                'sap',
                'commerce',
                'ccv2',
                'public cloud',
                'manifest'
            ])
        }
    }
}

sourceSets {
    commonTest {
        groovy.srcDir file('src/commonTest/groovy')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

sourceSets {
    integrationTest {
        groovy.srcDir file('src/integrationTest/groovy')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath + sourceSets.commonTest.output
        runtimeClasspath += output + compileClasspath
    }
}

tasks.register("integrationTest", Test.class) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter test
}

check.dependsOn integrationTest

sourceSets {
    functionalTest {
        groovy.srcDir file('src/functionalTest/groovy')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath + sourceSets.commonTest.output
        runtimeClasspath += output + compileClasspath
    }
}

tasks.register("functionalTest", Test.class) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    mustRunAfter test, integrationTest
}

check.dependsOn functionalTest

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events "passed", "failed", "skipped", "standardError"
        showStandardStreams = true
        showStackTraces = true
        showExceptions = true
    }
}
